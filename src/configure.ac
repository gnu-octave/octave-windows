dnl The configure script is generated by autogen.sh from configure.base 
dnl and the various configure.add files in the source tree.  Edit 
dnl configure.base and reprocess rather than modifying ./configure.

### Copyright (C) 2015-2019 Olaf Till <i7tiol@t-online.de>
### Copyright (C) 2020 John Donoghue <john.donoghue@ieee.org>
###
### This program is free software; you can redistribute it and/or
### modify it under the terms of the GNU General Public License as
### published by the Free Software Foundation; either version 3 of the
### License, or (at your option) any later version.
###
### This program is distributed in the hope that it will be useful,
### but WITHOUT ANY WARRANTY; without even the implied warranty of
### MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
### General Public License for more details.
###
### You should have received a copy of the GNU General Public License
### along with this program; if not, see
### <http://www.gnu.org/licenses/>.

dnl autoconf 2.13 certainly doesn't work! What is the minimum requirement?
AC_PREREQ(2.2)

AC_INIT([windows],[1.6.1])
AC_CONFIG_HEADERS([config.h])

# Avoid warnings for redefining AH-generated preprocessor symbols of
# Octave.
AH_TOP([#include "undef-ah-octave.h"])

AC_CONFIG_MACRO_DIRS([m4])

dnl Kill caching --- this ought to be the default
define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl

dnl uncomment to put support files in another directory
dnl AC_CONFIG_AUX_DIR(admin)

dnl if mkoctfile doesn't work, then we need the following:
dnl AC_PROG_CXX

dnl Need C compiler regardless so define it in a way that
dnl makes autoconf happy and we can override whatever we
dnl need with mkoctfile -p.
dnl XXX FIXME XXX should use mkoctfile to get CC and CFLAGS
AC_PROG_CC

AC_PROG_GREP

dnl Check if we are windows or not - COM only works in windows 
# Check for Windows
AC_MSG_CHECKING([for windows])
have_windows=no
AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[
   #include <windows.h>
   #ifndef __WIN32__
     #error "Not windows!"
   #endif
  ]], [])],
 [AC_MSG_RESULT([yes])
  have_windows=yes],
 [AC_MSG_RESULT([no])])

OLELIBS=
if test $have_windows = yes; then
  AC_DEFINE([USING_WINDOWS], [1], [are we a windows system])
  OLELIBS="-ladvapi32 -lole32 -loleaut32 -luuid"
  # prevent some warnings by defining this ...
  AC_DEFINE([_NO_SYS_GUID_OPERATOR_EQ_], [1], [Dont define guid == ops])
fi
AC_SUBST(OLELIBS)


dnl XXX FIXME XXX need tests for -p -c -s in mkoctfile.

dnl *******************************************************************
dnl Sort out mkoctfile version number and install paths

dnl XXX FIXME XXX latest octave has octave-config so we don't
dnl need to discover things here.  Doesn't have --exe-site-dir
dnl but defines --oct-site-dir and --m-site-dir

dnl Check for mkoctfile
AC_CHECK_PROG(MKOCTFILE,mkoctfile,mkoctfile)
test -z "$MKOCTFILE" &&	AC_MSG_WARN([no mkoctfile found on path])

AC_CHECK_PROG(OCTAVE_CONFIG,octave-config,octave-config)
test -z "$OCTAVE_CONFIG" && OCTAVE_CONFIG=$MKOCTFILE

dnl **********************************************************

# checks for depreciated octave symbols/functions

OCTAVE_HAS_FUNCTION([winqueryreg], [], [PKGWINQUERYREGFUNC=winqueryreg])
AC_SUBST(PKGWINQUERYREGFUNC)

AC_LANG_PUSH([C++])
save_altsyms_CXX="$CXX"
save_altsyms_CXXFLAGS="$CXXFLAGS"
save_altsyms_LDFLAGS="$LDFLAGS"
save_altsyms_LIBS="$LIBS"
OCTINCLUDEDIR=${OCTINCLUDEDIR:-`$MKOCTFILE -p INCFLAGS`}
OCTLIBDIR=${OCTLIBDIR:-`$MKOCTFILE -p OCTLIBDIR`}
CXX=`${MKOCTFILE} -p CXX`
CXXFLAGS="$OCTINCLUDEDIR $CXXFLAGS"
LDFLAGS="-L$OCTLIBDIR $LDFLAGS"
LIBS="-loctinterp $LIBS"

OF_OCTAVE_LIST_ALT_SYMS([
[dnl
  [flush_octave_stdout],
  [octave::flush_stdout],
  [[octave::flush_stdout ();]],
  [OCTAVE__FLUSH_STDOUT],
  [],
  []
],

[dnl
  [feval],
  [octave::feval],
  [[octave::feval ("date");]],
  [OCTAVE__FEVAL],
  [[#include <octave/parse.h>]],
  [[#include <octave/parse.h>]]
],

[dnl
  [octave_kbhit],
  [octave::kbhit],
  [[octave::kbhit (1);]],
  [OCTAVE__KBHIT],
  [[#include <octave/sysdep.h>]],
  [[#include <octave/sysdep.h>]]
],

[dnl
  [is_cell],
  [iscell],
  [[octave_value ().iscell ();]],
  [OV_ISCELL],
  [],
  []
],

[dnl
  [is_empty],
  [isempty],
  [[octave_value ().isempty ();]],
  [OV_ISEMPTY],
  [],
  []
],

[dnl
  [is_numeric_type],
  [isnumeric],
  [[octave_value ().isnumeric ();]],
  [OV_ISNUMERIC],
  [],
  []
]

],[oct-alt-includes.h])

OF_OCTAVE_CHECK_SYM(
  [for octave::sys::u8_to_wstring],
  [
    std::wstring s = octave::sys::u8_to_wstring("test");
  ],
  [HAVE_OCTAVE_U8_TO_WSTRING],
  [
    #include <octave/lo-sysdep.h>
  ]
)

CXX=$save_altsyms_CXX
CXXFLAGS=$save_altsyms_CXXFLAGS
LDFLAGS=$save_altsyms_LDFLAGS
LIBS=$save_altsyms_LIBS
AC_LANG_POP([C++])

AC_OUTPUT(Makefile ../INDEX)
AC_MSG_RESULT([
$PACKAGE_NAME is configured with
   mkoctfile:	$MKOCTFILE

including conditional functions
   $PKGWINQUERYREGFUNC
])
