dnl The configure script is generated by autogen.sh from configure.base 
dnl and the various configure.add files in the source tree.  Edit 
dnl configure.base and reprocess rather than modifying ./configure.

dnl autoconf 2.13 certainly doesn't work! What is the minimum requirement?
AC_PREREQ(2.2)

AC_INIT([windows],[1.3.0])
AC_CONFIG_HEADERS([config.h])

# Avoid warnings for redefining AH-generated preprocessor symbols of
# Octave.
AH_TOP([#include "undef-ah-octave.h"])

AC_CONFIG_MACRO_DIRS([m4])

dnl Kill caching --- this ought to be the default
define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl

dnl uncomment to put support files in another directory
dnl AC_CONFIG_AUX_DIR(admin)

dnl if mkoctfile doesn't work, then we need the following:
dnl AC_PROG_CXX

dnl Need C compiler regardless so define it in a way that
dnl makes autoconf happy and we can override whatever we
dnl need with mkoctfile -p.
dnl XXX FIXME XXX should use mkoctfile to get CC and CFLAGS
AC_PROG_CC

dnl Check if we are windows or not - COM only works in windows 
# Check for Windows
AC_MSG_CHECKING([for windows])
have_windows=no
AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[
   #include <windows.h>
   #ifndef __WIN32__
     #error "Not windows!"
   #endif
  ]], [])],
 [AC_MSG_RESULT([yes])
  have_windows=yes],
 [AC_MSG_RESULT([no])])

OLELIBS=
if test $have_windows = yes; then
  AC_DEFINE([USING_WINDOWS], [1], [are we a windows system])
  OLELIBS="-ladvapi32 -lole32 -loleaut32 -luuid"
fi
AC_SUBST(OLELIBS)


dnl XXX FIXME XXX need tests for -p -c -s in mkoctfile.

dnl *******************************************************************
dnl Sort out mkoctfile version number and install paths

dnl XXX FIXME XXX latest octave has octave-config so we don't
dnl need to discover things here.  Doesn't have --exe-site-dir
dnl but defines --oct-site-dir and --m-site-dir

AC_CHECK_PROG(OCTAVE,octave,octave)
test -z "$OCTAVE" && AC_MSG_ERROR([no octave found on path])

dnl Check for mkoctfile
AC_CHECK_PROG(MKOCTFILE,mkoctfile,mkoctfile)
test -z "$MKOCTFILE" &&	AC_MSG_WARN([no mkoctfile found on path])
AC_CHECK_PROG(OCTAVE_CONFIG,octave-config,octave-config)
test -z "$OCTAVE_CONFIG" &&	AC_MSG_WARN([no octave-config found on path])

AC_SUBST(ver)
AC_SUBST(mpath)
AC_SUBST(opath)
AC_SUBST(xpath)

AC_ARG_WITH(path, 
	[  --with-path             install path prefix],
	[ path=$withval ])
AC_ARG_WITH(mpath,
	[  --with-mpath            override path for m-files],
	[mpath=$withval])
AC_ARG_WITH(opath,
	[  --with-opath            override path for oct-files],
	[opath=$withval])
AC_ARG_WITH(xpath,
	[  --with-xpath            override path for executables],
	[xpath=$withval])

if test -n "$path" ; then
   test -z "$mpath" && mpath=$path 
   test -z "$opath" && opath=$path/oct 
   test -z "$xpath" && xpath=$path/bin
fi

dnl *******************************************************************

dnl XXX FIXME XXX Should we allow the user to override these?
dnl Do we even need them?  The individual makefiles can call mkoctfile -p
dnl themselves, so the only reason to keep them is for configure, and
dnl for those things which are not built using mkoctfile (e.g., aurecord)
dnl but it is not clear we should be using octave compile flags for those.

dnl C compiler and flags
AC_MSG_RESULT([retrieving compile and link flags from $MKOCTFILE])
CC=`$MKOCTFILE -p CC`
CFLAGS=`$MKOCTFILE -p CFLAGS`
CPPFLAGS=`$MKOCTFILE -p CPPFLAGS`
CPICFLAG=`$MKOCTFILE -p CPICFLAG`
LDFLAGS=`$MKOCTFILE -p LDFLAGS`
LIBS=`$MKOCTFILE -p LIBS`
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(CPICFLAG)

dnl C++ compiler and flags
CXX=`$MKOCTFILE -p CXX`
CXXFLAGS=`$MKOCTFILE -p CXXFLAGS`
CXXPICFLAG=`$MKOCTFILE -p CXXPICFLAG`
AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(CXXPICFLAG)

dnl *******************************************************************

dnl Check for features of your version of mkoctfile.
dnl All checks should be designed so that the default
dnl action if the tests are not performed is to do whatever
dnl is appropriate for the most recent version of Octave.

dnl Define the following macro:
dnl    OF_CHECK_LIB(lib,fn,true,false,helpers)
dnl This is just like AC_CHECK_LIB, but it doesn't update LIBS
AC_DEFUN([OF_CHECK_LIB],
[save_LIBS="$LIBS"
AC_CHECK_LIB($1,$2,$3,$4,$5)
LIBS="$save_LIBS"
])

dnl Define the following macro:
dnl    TRY_MKOCTFILE(msg,program,action_if_true,action_if_false)
dnl
AC_DEFUN([TRY_MKOCTFILE],
[AC_MSG_CHECKING($1)
cat > conftest.cc << EOF
#include <octave/config.h>
$2
EOF
ac_try="$MKOCTFILE -c conftest.cc"
if AC_TRY_EVAL(ac_try) ; then
   AC_MSG_RESULT(yes)
   $3
else
   AC_MSG_RESULT(no)
   $4
fi
])

dnl **********************************************************

dnl Evaluate an expression in octave
dnl
dnl OCTAVE_EVAL(expr,var) -> var=expr
dnl
AC_DEFUN([OCTAVE_EVAL],
[AC_MSG_CHECKING([for $1 in Octave])
$2=`echo "disp($1)" | $OCTAVE -qf`
AC_MSG_RESULT($$2)
AC_SUBST($2)
])

dnl Check status of an octave variable
dnl
dnl OCTAVE_CHECK_EXIST(variable,action_if_true,action_if_false)
dnl
AC_DEFUN([OCTAVE_CHECK_EXIST],
[AC_MSG_CHECKING([for $1 in Octave])
if test `echo 'disp(exist("$1"))' | $OCTAVE -qf`X != 0X ; then
   AC_MSG_RESULT(yes)
   $2
else
   AC_MSG_RESULT(no)
   $3
fi
])

AC_DEFUN([OCTAVE_CONFIG_EVAL],
[AC_MSG_CHECKING([for $1 in octave-config])
$2=`$OCTAVE_CONFIG -p $1`
AC_MSG_RESULT($$2)
AC_SUBST($2)
])

OCTAVE_CONFIG_EVAL(VERSION,OCTAVE_VERSION)
dnl get major ver part as used for a HAVE_OCTAVE_XXX define
ver=`echo $OCTAVE_VERSION | sed -e "s/\.//" -e "s/\..*$//"`

dnl grab canonical host type so we can write system specific install stuff
OCTAVE_CONFIG_EVAL(CANONICAL_HOST_TYPE,canonical_host_type)

if test -z "$mpath"; then
OCTAVE_CONFIG_EVAL(LOCALVERFCNFILEDIR,mpath)
fi
if test -z "$opath"; then
OCTAVE_CONFIG_EVAL(LOCALVEROCTFILEDIR,opath)
fi
if test -z "$xpath"; then
OCTAVE_CONFIG_EVAL(LOCALVERARCHLIBDIR,xpath)
fi

# checks for depreciated octave symbols/functions

PKGWINQUERYREGFUNC=
OCTAVE_CHECK_EXIST([winqueryreg],
  [],
  [PKGWINQUERYREGFUNC=winqueryreg]
)
AC_SUBST(PKGWINQUERYREGFUNC)

AC_LANG_PUSH([C++])
save_altsyms_CXX="$CXX"
save_altsyms_CXXFLAGS="$CXXFLAGS"
save_altsyms_LDFLAGS="$LDFLAGS"
save_altsyms_LIBS="$LIBS"
OCTINCLUDEDIR=${OCTINCLUDEDIR:-`$MKOCTFILE -p INCFLAGS`}
OCTLIBDIR=${OCTLIBDIR:-`$MKOCTFILE -p OCTLIBDIR`}
CXX=`${MKOCTFILE} -p CXX`
CXXFLAGS="$OCTINCLUDEDIR $CXXFLAGS"
LDFLAGS="-L$OCTLIBDIR $LDFLAGS"
LIBS="-loctinterp $LIBS"

OF_OCTAVE_LIST_ALT_SYMS([
[dnl
  [flush_octave_stdout],
  [octave::flush_stdout],
  [[octave::flush_stdout ();]],
  [OCTAVE__FLUSH_STDOUT],
  [],
  []
],

[dnl
  [feval],
  [octave::feval],
  [[octave::feval ("date");]],
  [OCTAVE__FEVAL],
  [[#include <octave/parse.h>]],
  [[#include <octave/parse.h>]]
],

[dnl
  [is_cell],
  [iscell],
  [[octave_value ().iscell ();]],
  [OV_ISCELL],
  [],
  []
],

[dnl
  [is_empty],
  [isempty],
  [[octave_value ().isempty ();]],
  [OV_ISEMPTY],
  [],
  []
]

],[oct-alt-includes.h])

CXX=$save_altsyms_CXX
CXXFLAGS=$save_altsyms_CXXFLAGS
LDFLAGS=$save_altsyms_LDFLAGS
LIBS=$save_altsyms_LIBS
AC_LANG_POP([C++])

AC_PROG_LN_S

AC_PROG_RANLIB

dnl Use $(COPY_FLAGS) to set options for cp when installing .oct files.
COPY_FLAGS="-Rfp"
case "$canonical_host_type" in
    *-*-linux*)
        COPY_FLAGS="-fdp"
    ;;
esac
AC_SUBST(COPY_FLAGS)

dnl Use $(STRIP) in the makefile to strip executables.  If not found, 
dnl STRIP expands to ':', which in the makefile does nothing.
dnl Don't need this for .oct files since mkoctfile handles them directly
STRIP=${STRIP-strip}
AC_CHECK_PROG(STRIP,$STRIP,$STRIP,:)

dnl Strip on windows, don't strip on Mac OS/X or IRIX
dnl For the rest, you can force strip using MKOCTFILE="mkoctfile -s"
dnl or avoid strip using STRIP=: before ./configure
case "$canonical_host_type" in
    powerpc-apple-darwin*|*-sgi-*)
	STRIP=:
    ;;
    *-cygwin-*|*-mingw-*) 
	MKOCTFILE="$MKOCTFILE -s" 
    ;;
esac

AC_OUTPUT(Makefile ../INDEX)
AC_MSG_RESULT([
octave commands will install into the following directories:
   m-files:   $mpath
   oct-files: $opath
   binaries:  $xpath

shell commands will install into the following directories:
   binaries:  $bindir
   man pages: $mandir
   libraries: $libdir
   headers:   $includedir

octave-forge is configured with
   octave:      $OCTAVE (version $OCTAVE_VERSION)
   mkoctfile:	$MKOCTFILE for Octave $OCTAVE_VERSION
   octave-config: $OCTAVE_CONFIG for Octave $OCTAVE_VERSION

including conditional functions
   $PKGWINQUERYREGFUNC
])
